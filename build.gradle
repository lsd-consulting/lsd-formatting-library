plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.10'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.palantir.git-version' version '4.3.0'
    id 'jacoco'
}

apply from: 'hooks.gradle'

group = 'io.github.lsd-consulting'
version = gitVersion().replaceAll("^v", "")
logger.lifecycle("Version=$version")

kotlin {
    jvmToolchain(17)
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    implementation 'io.github.lsd-consulting:lsd-logging-library:2.0.3'

    // JSON & XML pretty formatting
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation('org.dom4j:dom4j:2.2.0') {
        exclude group: 'pull-parser', module: 'pull-parser'
        because "pull-parser doesn't support the external-general-entities feature"
    }
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.21.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.21.0'

    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    testImplementation 'ch.qos.logback:logback-classic:1.5.31'
    testImplementation 'ch.qos.logback:logback-core:1.5.31'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
    testImplementation 'org.hamcrest:hamcrest-core:3.0'
    testImplementation 'com.approvaltests:approvaltests:25.10.0'
    testImplementation 'io.mockk:mockk-jvm:1.14.9'
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Unit test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}
check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/coverage")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                minimum = 0.96
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                minimum = 1.00
            }
        }
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.97
            }
        }
       rule {
            limit {
                counter = 'METHOD'
                minimum = 0.89
            }
        }
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-formatting-library'
            version = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-formatting-library'
                description = 'A library providing a formatting functionality, eg. JSON, XML.'
                url = 'https://github.com/lsd-consulting/lsd-formatting-library.git'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/lsd-consulting/lsd-formatting-library/blob/main/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Nick"
                        email = "nicholas.mcdowall@gmail.com"
                        organization = 'NKM IT Solutions'
                        organizationUrl = 'https://github.com/nickmcdowall'
                    }
                    developer {
                        name = "Lukasz"
                        email = "lukasz.gryzbon@gmail.com"
                        organization = 'Integreety Ltd.'
                        organizationUrl = 'https://www.integreety.co.uk'
                    }
                }
                scm {
                    url = "https://github.com/lsd-consulting/lsd-formatting-library.git"
                }
            }
            repositories {
                maven {
                    name = 'ossrh-staging-api'
                    url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

signing {
    if (project.findProperty("signingKey")) {
        // Use in-memory ascii-armored keys
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey as String, signingPassword as String)
        sign publishing.publications.mavenJava
    } else {
        // Use signing properties in ~/.gradle/gradle.properties
        sign publishing.publications.mavenJava
    }
}
